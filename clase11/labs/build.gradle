plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.springframework.cloud.contract' version '4.3.0'
}

group = 'com.circulosiete.curso.minibank'
version = '0.0.1-SNAPSHOT'
description = 'Clase 08 - Minibank'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

ext {
    springCloudVersion = "2025.0.0"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

    runtimeOnly 'org.springframework.boot:spring-boot-docker-compose'
    runtimeOnly 'org.postgresql:postgresql'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'

    testImplementation 'org.junit.platform:junit-platform-suite:1.13.4'

    testImplementation 'io.cucumber:cucumber-java:7.18.1'
    testImplementation 'io.cucumber:cucumber-spring:7.18.1'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.18.1'

    testImplementation 'io.rest-assured:rest-assured:5.5.0'
    testImplementation 'org.assertj:assertj-core:3.26.0'

    testImplementation 'au.com.dius.pact.consumer:junit5:4.6.17'
    testImplementation 'au.com.dius.pact.provider:junit5spring:4.6.17'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
    attributes(
        'snippets': snippetsDir,
        'doctype': 'book',
        'backend': 'html'
    )
}

contracts {
    testFramework = org.springframework.cloud.contract.verifier.config.TestFramework.JUNIT5
    failOnNoContracts = false
}
